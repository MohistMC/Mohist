import net.minecraftforge.forge.tasks.Util
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
}

group = 'com.mohistmc'
version = MOHIST_VERSION

configurations {
    mohistlauncher
    implementation.extendsFrom(mohistlauncher)
}

repositories {
    mavenCentral()
    maven { url 'https://maven.mohistmc.com/' }
}

dependencies {
    mohistlauncher(libs.yaml)
    mohistlauncher(libs.snakeyaml)
    mohistlauncher(libs.i18n)
    mohistlauncher(libs.json)
    mohistlauncher(libs.tools)
    mohistlauncher(libs.progressbar)
    mohistlauncher(libs.orgjline)
}

jar {
    into('data') {
        from configurations.mohistlauncher
    }
    from(rootProject.file('server_files/mohistlauncher.txt')) {
        filter(ReplaceTokens, tokens: [LIBRARIES: configurations.mohistlauncher.resolvedConfiguration.resolvedArtifacts.collect {
            "${Util.getMavenInfoFromDep(it).path}"
        }.join('\n')])
        filter(ReplaceTokens, tokens: [VERSION: MC_VERSION + "-" + MOHIST_VERSION])
        rename { 'mohistlauncher.txt' }
    }

    manifest {
        attributes([
                'Automatic-Module-Name': 'com.mohistmc.mohist.bootstrap'
        ] as LinkedHashMap)
        attributes([
                'Specification-Title'   : 'MohistMC',
                'Specification-Vendor'  : 'MohistMC',
                'Implementation-Title'  : 'Mohist',
                'Implementation-Version': MOHIST_VERSION,
                'Implementation-Vendor' : 'MohistMC'
        ] as LinkedHashMap, 'com/mohistmc/mohist/')
    }
}